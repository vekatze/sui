import {
  core.c-int {C-Int, from-c-int},
  core.errno {Errno, get-error-message},
  core.int.show {show-int},
  http-base.request {request},
  http-base.request.parse.v1-1 {request-parser},
  http-base.response {response, text-response},
  http-base.response.show.v1-1 {show-response},
  socket.address-family,
  socket.socket {Socket-Address, socket-address, start-server},
  socket.socket-type {SOCK_STREAM},
  this.util {_print-green-bold},
  zonk.parser {run},
  zonk.state {new-state},
}

data server {
| Server(
    address: &text,
    number-of-threads: int,
    port: int16,
    act: (request) -> response,
  )
}

inline _run(act: (request) -> response): (address: socket-address, text) -> text {
  function (address: socket-address, t: text) {
    let Socket-Address of {ip-address, port} = address in
    _print-green-bold("INFO: ");
    printf("{}:{}\n", [ip-address, show-int(magic cast(_, _, port))]);
    pin st = new-state(t) in
    match run(request-parser(), st) {
    | Right(req) =>
      let result = act(req) in
      show-response(result)
    | Left(_) =>
      show-response(text-response(400, *"The server couldn't parse the request\n"))
    }
  }
}

inline serve(s: server): unit {
  let Server of {address, number-of-threads, port, act} = s in
  let server-config =
    socket.socket.Config of {
      family = socket.address-family.AF_INET,
      comm-type = SOCK_STREAM,
      reuse-socket = True,
      protocol = C-Int(0),
      port,
      address,
      backlog = C-Int(128),
      threads = number-of-threads,
      interpreter = _run(act),
    }
  in
  match start-server(server-config) {
  | Left(Errno(i)) =>
    printf("failed to start a server.\nerror: {} {}\n", [show-int(from-c-int(i)), get-error-message(Errno(i))])
  | Right(_) =>
    Unit
  }
}
